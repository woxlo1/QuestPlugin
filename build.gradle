plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.8.22'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group = 'com.woxloi'
version = '2.40'

repositories {
    mavenCentral()
    maven { url = 'https://repo.papermc.io/repository/maven-public/' }
    maven { url = "https://repo.citizensnpcs.co/" }
    maven { url = 'https://jitpack.io' }
    maven { url = 'https://mvn.lumine.io/repository/maven-public/' }
    maven { url = 'https://maven.enginehub.org/repo/' }
    maven { url = "https://repo.codemc.io/repository/maven-public/" }
}


dependencies {
    // Paper API
    compileOnly "io.papermc.paper:paper-api:1.20.4-R0.1-SNAPSHOT"

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    // MythicBukkit API
    compileOnly "io.lumine:Mythic-Dist:5.7.1"

    // Vault
    compileOnly "com.github.MilkBowl:VaultAPI:1.7"

    // Adventure
    implementation "net.kyori:adventure-platform-bukkit:4.3.1"

    // Coroutine
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.2"

    implementation "com.zaxxer:HikariCP:5.0.1" // コネクションプール
    implementation "mysql:mysql-connector-java:8.0.33"

    // Citizens
    compileOnly "net.citizensnpcs:citizensapi:2.0.31-SNAPSHOT"

    implementation "org.graalvm.polyglot:polyglot:23.1.0"
    implementation "org.graalvm.polyglot:js:23.1.0"

    // libs フォルダの jar
    implementation fileTree(dir: 'libs', include: '*.jar')
}

def targetJavaVersion = 17
java {
    sourceCompatibility = JavaVersion.toVersion(targetJavaVersion)
    targetCompatibility = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < JavaVersion.toVersion(targetJavaVersion)) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "17"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "17"
    }
}

shadowJar {
    archiveFileName.set("${project.name}-${project.version}.jar")
}
